plugins {
  id 'java'
  id 'java-library'
  id 'maven'
  id 'signing'
  id "com.palantir.git-version" version "0.12.0-rc2"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
  mavenLocal()
  jcenter()
}

test {
  useTestNG()
}

group = "com.dronjax.java"
archivesBaseName = "object-shutdown-proxy"
version gitVersion()

dependencies {
  implementation "org.slf4j:slf4j-log4j12:1.7.12"
  implementation 'com.google.guava:guava:23.0'

  testImplementation "org.testng:testng:6.14.3"
  testImplementation "org.mockito:mockito-core:1.10.19"
  testImplementation "org.apache.logging.log4j:log4j-slf4j18-impl:2.11.1"
}

jar {
  manifest {
    attributes('Implementation-Title': project.name,
               'Implementation-Version': project.version)
  }
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar, jar
}

signing {
  useGpgCmd()
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'object-shutdown-proxy'
        packaging 'jar'
        description 'Create proxy for your java object to synchronously wait for all methods to finish calling.'
        url 'https://github.com/dronjax/java-object-shutdown-proxy'

        scm {
          connection 'scm:git:https://github.com/dronjax/java-object-shutdown-proxy.git'
          developerConnection 'scm:git:git@github.com:dronjax/java-object-shutdown-proxy.git'
          url 'https://github.com/dronjax/java-object-shutdown-proxy'
        }

        licenses {
          license {
            name 'GNU General Public License v3.0'
            url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
          }
        }

        developers {
          developer {
            id 'dronjax'
            name 'Jordan Fernando'
            email 'mail@dronjax.com'
          }
        }
      }
    }
  }
}

